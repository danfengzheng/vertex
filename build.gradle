plugins {
    id 'java'
    id 'java-library'
    alias(libs.plugins.spring.boot) apply(false)
    alias(libs.plugins.spring.dependency.management)
}

// 所有项目通用配置
allprojects {
    group = 'com.vertex'
    version = '1.0.0'

    repositories {
        // 阿里云Maven仓库
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        mavenCentral()
    }
}

// 所有子项目通用配置
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    // JDK 21配置
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    // 启用JDK 21新特性（可选）
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
        // 如果要使用预览特性，取消下面注释
        // options.compilerArgs << '--enable-preview'
    }

    // 依赖管理
    dependencyManagement {
        imports {
            // 使用 libs.versions.toml 中定义的 BOM
            mavenBom libs.spring.boot.dependencies.get().toString()
            mavenBom libs.spring.cloud.dependencies.get().toString()
            mavenBom libs.spring.cloud.alibaba.dependencies.get().toString()
        }
    }

    // 公共依赖
    dependencies {
        // Lombok
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        // 测试
        testImplementation libs.spring.boot.starter.test
        testImplementation libs.junit.jupiter
    }

    // 测试配置
    test {
        useJUnitPlatform()
        // 如果使用了预览特性
        // jvmArgs '--enable-preview'
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}

// 根项目不打包
tasks.named('jar') {
    enabled = false
}